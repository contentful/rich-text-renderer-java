plugins {
    id("com.android.library")
    id("maven-publish")
}

repositories {
    maven { url 'https://jitpack.io' }
}

android {
    compileSdk 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

dependencies {
    implementation project(path: ':core')

    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation "com.contentful.java:java-sdk:${project.contentful_version}"
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation 'org.robolectric:robolectric:4.9'
    testImplementation 'com.google.truth:truth:0.42'
    testImplementation 'junit:junit:4.13.2'
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.release
                groupId = project.group
                artifactId = 'android'
                version = project.version
            }
        }
    }
}

if (android.productFlavors.size() > 0) {
    android.libraryVariants.all { variant ->
        if (variant.name.toLowerCase().contains("debug")) {
            return
        }

        def bundleTask = tasks["bundle${variant.name.capitalize()}"]

        artifacts {
            archives(bundleTask.archivePath) {
                archiveClassifier = variant.flavorName
                builtBy bundleTask
                name = project.name
            }
        }

    }
}
